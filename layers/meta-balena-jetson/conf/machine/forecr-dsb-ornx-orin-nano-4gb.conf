#@TYPE: Machine
#@NAME: Forecr DSBOARD ORNX / Jetson Orin Nano 4GB
##@DESCRIPTION: ForeCR DSB ORNX carrier board with Orin Nano 4GB
#

MACHINEOVERRIDES = "forecr-dsb-ornx-orin-nano-4gb:forecr-dsb-ornx-orin-nano:forecr-dsb-ornx"
# orin-nano.inc will prepend "orin-nano" to MACHINEOVERRIDES

# Refer to meta-tegra/conf/machine/p3768-0000-p3767-0004.conf for the following values
# Orin Nano 8GB is the default Nano in conf/machine/jetson-orin-nano-devkit-nvme.conf

TNSPEC_BOOTDEV ?= "nvme0n1p1"

PARTITION_LAYOUT_TEMPLATE_DEFAULT ?= "flash_t234_qspi.xml"
PARTITION_LAYOUT_TEMPLATE_DEFAULT_SUPPORTS_REDUNDANT ?= "1"
PARTITION_LAYOUT_EXTERNAL_DEFAULT ?= "flash_l4t_t234_nvme.xml"
TEGRAFLASH_NO_INTERNAL_STORAGE = "1"

# BOARDID-BOARDSKU
# p3767-0003 = Orin Nano 8GB
# p3767-0004 = Orin Nano 4GB
# p3767-0005 = Orin Nano 8GB + SD (devkit version)
#
# see https://docs.nvidia.com/jetson/archives/r36.2/DeveloperGuide/index.html#devices-supported-by-this-document

TEGRA_BOARDSKU = "0004"
KERNEL_DEVICETREE ?= "tegra234-p3768-0000+p3767-0004-nv.dtb"
# Use the values from p3768-0000-p3767-0004.conf for the 4GB SKU
NVPMODEL ?= "nvpmodel_p3767_0004"
EMMC_BCT ?= "tegra234-p3767-0004-sdram-l4t.dts"

require conf/machine/include/orin-nano.inc
require conf/machine/include/devkit-wifi.inc
